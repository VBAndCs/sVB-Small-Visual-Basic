Segoe UI
20
False
False
False
#FF000000
#FFFFFFFF
True
True
False
اكتب السطر "                     أ       ج"
اكتب السطر "حساب قيمة   ـــ  +  ــــ"
اكتب السطر "                    ب      د"
اكتب سطرا

# قراءة قيم أ و ب و ج و د:
اكتب "أدخل قيمة أ: "
أ = اقرأ عددا

اكتب "أدخل قيمة ب: "
بينما نعم
    ب = اقرأ عددا
    لو ب = 0
        اكتب "غير مسموح بصفر في المقام.. أدخل قيمة أخرى: "
    وإلا
        اخرج
    فقط
كرر

اكتب "أدخل قيمة ج: "
ج = اقرأ عددا

اكتب "أدخل قيمة د: "
بينما نعم
    د = اقرأ عددا
    لو د = 0
        اكتب "غير مسموح بصفر في المقام.. أدخل قيمة أخرى: "
    وإلا
        اخرج
    فقط
كرر

# حساب الكسر الناتج
لو ب = د
    بسط = أ + ج
    مقام = ب
أو لو ب % د = 0
    بسط = أ + ج × ب ÷ د
    مقام = ب
أو لو د % ب = 0
    بسط = أ × د ÷ ب + ج
    مقام = د
وإلا
    بسط = أ × د + ب × ج
    مقام = ب × د
فقط

# طباعة النتيجة
اكتب أ + "\" + ب
اكتب " + "
اكتب ج + "\" + د
اكتب " = "
اكتب بسط + "\" + مقام

# تبسيط الكسر الاعتيادي

لو بسط = 0
    اكتب " = 0"
وإلا
    لو بسط > مقام
        صحيح = بسط ÷ مقام
        باقي = بسط % مقام
        اكتب " = " + صحيح
        لو باقي > 0
            أ = باقي
            ب = مقام
            اكتب " + " + أ + "\" + ب
        وإلا
            أ = 1
            ب = 1
        فقط
    وإلا
        صحيح = 0
        أ = بسط
        ب = مقام
    فقط
    
    # حساب القاسم المشترك الأكبر للبسط والمقام:
    بسط = أ
    مقام = ب
    بينما مقام > 0
        باقي = بسط % مقام
        بسط = مقام
        مقام = باقي
    كرر
    
    القاسم = بسط
    لو القاسم > 1
        اكتب " = "
        لو صحيح > 0
            اكتب صحيح + " + "
        فقط
        اكتب السطر أ / القاسم + "\" + ب / القاسم
    فقط
فقط