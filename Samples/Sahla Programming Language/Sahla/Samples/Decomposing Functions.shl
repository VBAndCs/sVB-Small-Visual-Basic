Segoe UI
16
False
False
False
#FF000000
#FFFFFFFF
True
True
False
اكتب السطر "برنامج لتحليل دالة كثيرة الحدود من الدرجة الثانية على الصيغة:"
اكتب السطر "د(س) = أ س^2 + ب س + ج"
اكتب سطرا

بينما نعم
    اكتب "أدخل قيمة أ: "
    أ = اقرأ عددا
    اكتب "أدخل قيمة ب: "
    ب = اقرأ عددا
    اكتب "أدخل قيمة ج: "
    ج = اقرأ عددا
    
    # طباعة صيغة الدالة:
    دالة = ""
    
    لو أ = 0 - 1
        دالة = "-س^2"
    أو لو أ = 1
        دالة = "س^2"
    أو لو أ < > 0
        دالة = أ + " س^2"
    فقط
    
    لو دالة = ""
        لو ب = 0 - 1
            دالة = "-س"
        أو لو ب = 1
            دالة = "س"
        أو لو ب < > 0
            دالة = ب + " س"
        فقط
    أو لو ب = 0 - 1
        دالة = دالة + " - س"
    أو لو ب = 1
        دالة = دالة + " + س"
    أو لو ب < 0
        دالة = دالة + " - " + ( 0 - ب ) + " س"
    أو لو ب > 0
        دالة = دالة + " + " + ب + " س"
    فقط
    
    لو دالة = ""
        دالة = ج
    أو لو ج < 0
        دالة = دالة + " - " + ( 0 - ج )
    أو لو ج > 0
        دالة = دالة + " + " + ج
    فقط
    
    اكتب السطر "د(س) = " + دالة
    
    تحليل = لا
    
    لو أ < > 0
        لو ج = 0
            ف1 = ب
            ف2 = 0
            تحليل = نعم
        وإلا
            أج = أ * ج
            # تحديد حدود حلقة التكرار بناءً على قيمة أج
            لو أج < 0
                بداية = أج
                نهاية = 0 - أج
            وإلا
                بداية = 0 - أج
                نهاية = أج
            فقط
            
            من ن = بداية إلى نهاية
                لو ( ن < > 0 ) و ( أج % ن = 0 )
                    قاسم = أج / ن
                    لو ( ن + قاسم = ب )
                        ف1 = ن
                        ف2 = قاسم
                        تحليل = نعم
                        اخرج
                    فقط
                فقط
            زد ن
        فقط
    فقط
    
    لو تحليل
        # إعادة كتابة البسط بالشكل الوسيط: أ س^2 + ف1 س + ف2 س + ج
        # وهو يساوي س (أس+ف1) + (ف2 س + ج)
        # ونريد تحليله إلى  (م1 س + ع1)(م2 س + ع2)
        # وهذا يوجب إيجاد القاسم المشترك بين أ و ف1 (وسنسميه ق1)
        # والقاسم المشترك بين ج و ف2 (وسنسميه ق2)
        # بحيث يكون:
        # م1 = ق1
        # ع1 =  ق2
        # م2 = ف2/ق2
        # ع2 = ج /ق2
        
        # إيجاد ق1
        لو أ < 0
            البسط = 0 - أ
        وإلا
            البسط = أ
        فقط
        
        لو ف1 < 0
            المقام = 0 - ف1
        وإلا
            المقام = ف1
        فقط
        
        بينما المقام > 0
            الباقي = البسط % المقام
            البسط = المقام
            المقام = الباقي
        كرر
        
        ق1 = البسط
        لو أ < 0
            ق1 = 0 - ق1
        فقط
        
        # إيجاد ق2
        لو ف2 = 0
            ق2 = 0
        وإلا
            لو ج < 0
                البسط = 0 - ج
            وإلا
                البسط = ج
            فقط
            
            لو ف2 < 0
                المقام = 0 - ف2
            وإلا
                المقام = ف2
            فقط
            
            بينما المقام > 0
                الباقي = البسط % المقام
                البسط = المقام
                المقام = الباقي
            كرر
            
            ق2 = البسط
            لو ف2 < 0
                ق2 = 0 - ق2
            فقط
        فقط
        
        م1 = ق1
        ع1 = ق2
        لو ج < > 0
            م2 = ف2 / ق2
            ع2 = ج / ق2
        وإلا
            م2 = أ / ق1
            ع2 = ب / ق1
        فقط
        
        # تنسيق العامل الأول (م1 س + ع1)
        لو م1 = 1
            عامل1 = "س"
        أو لو م1 = 0 - 1
            عامل1 = "-س"
        وإلا
            عامل1 = م1 + " س"
        فقط
        
        لو ع1 > 0
            عامل1 = عامل1 + " + " + ع1
        أو لو ع1 < 0
            عامل1 = عامل1 + " - " + ( 0 - ع1 )
        فقط
        
        # تنسيق العامل الثاني (م2 س + ع2)
        لو م2 = 1
            عامل2 = "س"
        أو لو م2 = 0 - 1
            عامل2 = "-س"
        وإلا
            عامل2 = م2 + " س"
        فقط
        
        لو ع2 > 0
            عامل2 = عامل2 + " + " + ع2
        أو لو ع2 < 0
            عامل2 = عامل2 + " - " + ( 0 - ع2 )
        فقط
        
        اكتب السطر "د(س) = (" + عامل1 + ") (" + عامل2 + ")"
        لو م1 = م2 و ع1 = ع2
            اكتب السطر "د(س) = (" + عامل1 + ")^2"
        فقط
    فقط
    
    اكتب السطر "-------------------------------------------"
كرر